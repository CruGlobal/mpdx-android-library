// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        deps = [
                advrecyclerview    : '1.0.0',
                butterknife        : '10.2.3',
                dagger             : '2.37',
                eventbus           : '3.1.1',
                facebookFlipper    : '0.96.1',
                facebookSoloader   : '0.10.1',
                flipperRealm       : '2.1.0',
                googleTagManager   : '17.0.0',
                gtoSupport         : '3.8.1',
                guava              : '30.1.1-android',
                hamcrest           : '2.2',
                jacoco             : '0.8.7',
                junit              : '4.13.2',
                kotlin             : '1.5.10',
                kotlinCoroutines   : '1.5.0',
                leakcanary         : '2.7',
                materialDesign     : '1.3.0',
                mockito            : '3.11.2',
                mockitoKotlin      : '2.1.0',
                moshi              : '1.12.0',
                okhttp3            : '4.9.1',
                mpChart            : '3.1.0',
                picasso            : '2.71828',
                picassoTransforms  : '2.4.0',
                realm              : '10.6.0',
                realmAdapters      : '4.0.0',
                realmFieldNames    : '2.0.0',
                retrofit           : '2.9.0',
                roboelectric       : '4.6.1',
                splitties          : '3.0.0-beta01',
                timber             : '4.7.1',
        ]
        deps.androidX = [
                activity          : '1.2.3',
                appCompat         : '1.3.0',
                biometric         : '1.1.0',
                cardView          : '1.0.0',
                constraintLayout  : '2.0.4',
                core              : '1.5.0',
                fragment          : '1.3.5',
                lifecycle         : '2.3.1',
                multidex          : '2.0.1',
                swipeRefreshLayout: '1.1.0',
                test              : '1.4.0',
                testJUnit         : '1.1.3',
        ]
        deps.firebase = [
                appdistribution: '2.1.3',
                core           : '19.0.0',
                crashlytics    : '18.1.0',
                messaging      : '21.1.0',
                perf           : '20.0.1',
        ]
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "com.google.dagger:hilt-android-gradle-plugin:${deps.dagger}"
        classpath "com.google.firebase:firebase-appdistribution-gradle:${deps.firebase.appdistribution}"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
        classpath 'com.google.gms:google-services:4.3.8'
        classpath "io.realm:realm-gradle-plugin:${deps.realm}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${deps.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
        classpath "com.jakewharton:butterknife-gradle-plugin:${deps.butterknife}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.vanniktech.android.junit.jacoco" version "0.16.0"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    group 'org.cru.mpdx.android'
    version '1.0.0-SNAPSHOT'


    apply plugin: 'maven-publish'
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release

                    groupId = project.group
                    artifactId = project.name
                    version = project.version

                    pom.withXml {
                        def scm = asNode().appendNode('scm')
                        scm.appendNode('connection', POM_SCM_CONNECTION)
                        scm.appendNode('developerConnection', POM_SCM_CONNECTION)
                    }

                }
            }
            repositories {
                maven {
                    name "cruGlobalMavenRepository"

                    if (project.version.endsWith('-SNAPSHOT')) {
                        url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-snapshots-local/"
                    } else {
                        url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-releases-local/"
                    }

                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task jacocoTestReport {
    subprojects.each { dependsOn(it.tasks.withType(JacocoReport)) }
}
